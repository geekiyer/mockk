buildscript {
    ext.kotlin_gradle_version = findProperty('kotlin.version')?.toString() ?: '1.6.0'
    ext.android_gradle_version = '7.0.0'
    ext.byte_buddy_version = '1.12.6'
    ext.coroutines_version = '1.3.3'
    ext.dexmaker_version = '2.28.1'
    ext.objenesis_version = '3.1'
    ext.objenesis_android_version = '2.6'
    ext.junit_jupiter_version = '5.6.2'
    ext.junit_vintage_version = '5.6.2'
    ext.dokka_version = '1.6.0'
    ext.gradles = project.projectDir.toString() + "/gradle"

    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_gradle_version"
        classpath "com.android.tools.build:gradle:$android_gradle_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

subprojects { subProject ->
    group = 'io.mockk'

    ext.kotlin_version = findProperty('kotlin.version')?.toString() ?: '1.6.0'
    ext.kotlin_gradle_version = findProperty('kotlin.version')?.toString() ?: '1.6.0'

    repositories {
        mavenCentral()
        google()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
            apiVersion = "1.4"
            useIR = findProperty("kotlin.ir.enabled")?.toBoolean() == true
        }
    }
}
